export LIB=$HOME/.dotfiles/lib

export PROJECTS=~/Code

if [[ -a ~/.localrc ]]
then
    source ~/.localrc
fi

typeset -U config_files

config_files=($LIB/*/*.zsh)

for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

source $LIB/zsh/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

unset config_files
# export PATH=/Users/xavier/.local/bin:$PATH

[ -f ~/.fzf.zsh ] && source ~/.fzf.zsh
